# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=net-tools-selinux
pkgver=1.60.20130531git
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://net-tools.sourceforge.net/"
depends=('glibc' 'libselinux')
makedepends=('git')
# use git snapshot currently prefered over release
source=("net-tools::git://git.code.sf.net/p/net-tools/code#commit=6a14c03f"
        "config.in.patch")
options=(!makeflags)
sha1sums=('SKIP'
          '0618c4530045fdd8c0391a0e9bd207fd4cd1cef1')
groups=('selinux')
replaces=('net-tools')
conflicts=('net-tools' "${pkgname/-selinux}")
provides=('net-tools' "${pkgname/-selinux}=${pkgver}-${pkgrel}")

prepare() {
  sed -i "s#/sbin#/bin#" ${srcdir}/${pkgname/-selinux}/Makefile
  sed -i "s#/usr##" ${srcdir}/${pkgname/-selinux}/man/Makefile
}

build() {
  cd ${srcdir}/${pkgname/-selinux}
  patch < ../config.in.patch
  yes "" | make
}

package() {
  cd ${srcdir}/${pkgname/-selinux}
  make DESTDIR=${pkgdir}/usr update

  # the following is provided by yp-tools
  rm "${pkgdir}"/usr/bin/{nis,yp}domainname
  rm "${pkgdir}"/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm "${pkgdir}"/usr/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}
